plugins {
    id 'com.android.application'
    id 'kotlin-android'
    id 'kotlin-kapt'
    id 'com.google.devtools.ksp'
    id 'android.aop'//æœ€å¥½æ”¾åœ¨æœ€åä¸€è¡Œ
}

apply from : '../common.gradle'
// Android ä»£ç è§„èŒƒæ–‡æ¡£ï¼šhttps://github.com/getActivity/AndroidCodeStandard
android {

    // èµ„æºç›®å½•å­˜æ”¾æŒ‡å¼•ï¼šhttps://developer.android.google.cn/guide/topics/resources/providing-resources
    defaultConfig {

        // æ— ç—›ä¿®æ”¹åŒ…åï¼šhttps://www.jianshu.com/p/17327e191d2e
        applicationId 'com.hjq.demo'
        resourceConfigurations += ['zh', 'xxhdpi']

        // ä»…ä¿ç•™ä¸­æ–‡è¯­ç§çš„èµ„æº

        // ä»…ä¿ç•™ xxhdpi å›¾ç‰‡èµ„æºï¼ˆç›®å‰ä¸»æµåˆ†è¾¨ç‡ 1920 * 1080ï¼‰

        // æ··æ·†é…ç½®
        proguardFiles 'proguard-sdk.pro', 'proguard-app.pro'

        // æ—¥å¿—æ‰“å°å¼€å…³
        buildConfigField('boolean', 'LOG_ENABLE', '' + LOG_ENABLE + '')
        // æµ‹è¯•åŒ…ä¸‹çš„ BuglyId
        buildConfigField('String', 'BUGLY_ID', '"' + BUGLY_ID + '"')
        // æµ‹è¯•æœåŠ¡å™¨çš„ä¸»æœºåœ°å€
        buildConfigField('String', 'HOST_URL', '"' + HOST_URL + '"')
    }

    // Apk ç­¾åçš„é‚£äº›äº‹ï¼šhttps://www.jianshu.com/p/a1f8e5896aa2
    signingConfigs {
        config {
            storeFile file(StoreFile)
            storePassword StorePassword
            keyAlias KeyAlias
            keyPassword KeyPassword
        }
    }
    packagingOptions {
        jniLibs {
            excludes += ['META-INF/*******']
        }
        resources {
            excludes += ['META-INF/*******']
        }
    }

    // æ„å»ºé…ç½®ï¼šhttps://developer.android.google.cn/studio/build/build-variants
    buildTypes {

        debug {
            // ç»™åŒ…åæ·»åŠ åç¼€
            applicationIdSuffix '.debug'
            // è°ƒè¯•æ¨¡å¼å¼€å…³
            debuggable true
            jniDebuggable true
            // ç§»é™¤æ— ç”¨çš„èµ„æº
            shrinkResources false
            // ä»£ç æ··æ·†å¼€å…³
            minifyEnabled false
            // ç­¾åä¿¡æ¯é…ç½®
            signingConfig signingConfigs.config
            // æ·»åŠ æ¸…å•å ä½ç¬¦
            addManifestPlaceholders([
                    'app_name'    :    'å®‰å“æŠ€æœ¯ä¸­å° Debug ç‰ˆ'
            ])
            // è°ƒè¯•æ¨¡å¼ä¸‹åªä¿ç•™ä¸€ç§æ¶æ„çš„ so åº“ï¼Œæå‡æ‰“åŒ…é€Ÿåº¦
            ndk {
                abiFilters 'armeabi-v7a'
            }
        }

        preview.initWith(debug)
        preview {
            applicationIdSuffix ''
            // æ·»åŠ æ¸…å•å ä½ç¬¦
            addManifestPlaceholders([
                    'app_name'    :    'å®‰å“æŠ€æœ¯ä¸­å° Preview ç‰ˆ'
            ])
        }

        release {
            // è°ƒè¯•æ¨¡å¼å¼€å…³
            debuggable false
            jniDebuggable false
            // ç§»é™¤æ— ç”¨çš„èµ„æº
            shrinkResources true
            // ä»£ç æ··æ·†å¼€å…³
            minifyEnabled true
            // ç­¾åä¿¡æ¯é…ç½®
            signingConfig signingConfigs.config
            // æ·»åŠ æ¸…å•å ä½ç¬¦
            addManifestPlaceholders([
                    'app_name'    :    '@string/app_name'
            ])
            // ä»…ä¿ç•™ä¸¤ç§æ¶æ„çš„ so åº“ï¼Œæ ¹æ® Bugly ç»Ÿè®¡å¾—å‡º
            ndk {
                // armeabiï¼šä¸‡é‡‘æ²¹æ¶æ„å¹³å°ï¼ˆå ç”¨ç‡ï¼š0%ï¼‰
                // armeabi-v7aï¼šæ›¾ç»ä¸»æµçš„æ¶æ„å¹³å°ï¼ˆå ç”¨ç‡ï¼š10%ï¼‰
                // arm64-v8aï¼šç›®å‰ä¸»æµæ¶æ„å¹³å°ï¼ˆå ç”¨ç‡ï¼š95%ï¼‰
                abiFilters 'armeabi-v7a', 'arm64-v8a'
            }
        }
    }

    androidAopConfig {
        // enabled ä¸º false åˆ‡é¢ä¸å†èµ·ä½œç”¨ï¼Œé»˜è®¤ä¸å†™ä¸º true
        enabled true
        // include ä¸è®¾ç½®é»˜è®¤å…¨éƒ¨æ‰«æï¼Œè®¾ç½®ååªæ‰«æè®¾ç½®çš„åŒ…åçš„ä»£ç 
        include android.defaultConfig.applicationId
        // exclude æ˜¯æ‰«ææ—¶æ’é™¤çš„åŒ…
        // å¯æ’é™¤ kotlin ç›¸å…³ï¼Œæé«˜é€Ÿåº¦
        exclude 'kotlin.jvm', 'kotlin.internal','kotlinx.coroutines.internal', 'kotlinx.coroutines.android'

        // verifyLeafExtends æ˜¯å¦å¼€å¯éªŒè¯å¶å­ç»§æ‰¿ï¼Œé»˜è®¤æ‰“å¼€ï¼Œ@AndroidAopMatchClassMethod å’Œ @AndroidAopCollectMethod å¦‚æœæ²¡æœ‰è®¾ç½® LEAF_EXTENDSï¼Œå¯ä»¥å…³é—­
        verifyLeafExtends true
        //é»˜è®¤å…³é—­ï¼Œå¼€å¯åœ¨ Build æˆ– æ‰“åŒ…å å°†ä¼šç”Ÿæˆåˆ‡ç‚¹ä¿¡æ¯jsonæ–‡ä»¶åœ¨ app/build/tmp/cutInfo.json
        cutInfoJson false
    }

    namespace 'com.hjq.demo'
    lint {
        disable 'HardcodedText', 'ContentDescription'
    }

    applicationVariants.configureEach { variant ->
        // apk è¾“å‡ºæ–‡ä»¶åé…ç½®
        variant.outputs.configureEach { output ->
            outputFileName = rootProject.getName() + '_v' + variant.versionName + '_' + variant.buildType.name
            if (variant.buildType.name == buildTypes.release.getName()) {
                outputFileName += '_' + new Date().format('MMdd')
            }
            outputFileName += '.apk'
        }
    }
}

// æ·»åŠ æ„å»ºä¾èµ–é¡¹ï¼šhttps://developer.android.google.cn/studio/build/dependencies
// api ä¸ implementation çš„åŒºåˆ«ï¼šhttps://www.jianshu.com/p/8962d6ba936e
dependencies {
    // åŸºç±»å°è£…
    implementation project(':library:base')
    // æ§ä»¶å°è£…
    implementation project(':library:widget')
    // å‹ç›Ÿå°è£…
    implementation project(':library:umeng')

    // æƒé™è¯·æ±‚æ¡†æ¶ï¼šhttps://github.com/getActivity/XXPermissions
    implementation 'com.github.getActivity:XXPermissions:20.0'

    // æ ‡é¢˜æ æ¡†æ¶ï¼šhttps://github.com/getActivity/TitleBar
    implementation 'com.github.getActivity:TitleBar:9.2'

    // åå¸æ¡†æ¶ï¼šhttps://github.com/getActivity/Toaster
    implementation 'com.github.getActivity:Toaster:12.6'

    // ç½‘ç»œè¯·æ±‚æ¡†æ¶ï¼šhttps://github.com/getActivity/EasyHttp
    implementation 'com.github.getActivity:EasyHttp:10.2'
    // OkHttp æ¡†æ¶ï¼šhttps://github.com/square/okhttp
    // noinspection GradleDependency
    implementation 'com.squareup.okhttp3:okhttp:3.12.13'

    // Json è§£ææ¡†æ¶ï¼šhttps://github.com/google/gson
    implementation 'com.google.code.gson:gson:2.8.8'
    // Gson è§£æå®¹é”™ï¼šhttps://github.com/getActivity/GsonFactory
    implementation 'com.github.getActivity:GsonFactory:5.2'

    // Shape æ¡†æ¶ï¼šhttps://github.com/getActivity/ShapeView
    implementation 'com.github.getActivity:ShapeView:6.0'

    // AOP æ’ä»¶åº“ï¼šhttps://mvnrepository.com/artifact/org.aspectj/aspectjrt
//    implementation 'org.aspectj:aspectjrt:1.9.6'

    // å›¾ç‰‡åŠ è½½æ¡†æ¶ï¼šhttps://github.com/bumptech/glide
    // å®˜æ–¹ä½¿ç”¨æ–‡æ¡£ï¼šhttps://github.com/Muyangmin/glide-docs-cn
    implementation 'com.github.bumptech.glide:glide:4.12.0'
    kapt 'com.github.bumptech.glide:compiler:4.12.0'

    // æ²‰æµ¸å¼æ¡†æ¶ï¼šhttps://github.com/gyf-dev/ImmersionBar
    implementation 'com.gyf.immersionbar:immersionbar:3.0.0'

    // æ‰‹åŠ¿ ImageViewï¼šhttps://github.com/Baseflow/PhotoView
    implementation 'com.github.Baseflow:PhotoView:2.3.0'

    // Bugly å¼‚å¸¸æ•æ‰ï¼šhttps://bugly.qq.com/docs/user-guide/instruction-manual-android/?v=20190418140644
    implementation 'com.tencent.bugly:crashreport:3.4.4'
    implementation 'com.tencent.bugly:nativecrashreport:3.9.2'

    // åŠ¨ç”»è§£æåº“ï¼šhttps://github.com/airbnb/lottie-android
    // åŠ¨ç”»èµ„æºï¼šhttps://lottiefiles.comã€https://icons8.com/animated-icons
    implementation 'com.airbnb.android:lottie:4.1.0'

    // ä¸Šæ‹‰åˆ·æ–°ä¸‹æ‹‰åŠ è½½æ¡†æ¶ï¼šhttps://github.com/scwang90/SmartRefreshLayout
    implementation 'com.scwang.smart:refresh-layout-kernel:2.0.3'
    implementation 'com.scwang.smart:refresh-header-material:2.0.3'

    // æ—¥å¿—æ‰“å°æ¡†æ¶ï¼šhttps://github.com/JakeWharton/timber
    implementation 'com.jakewharton.timber:timber:4.7.1'

    // æŒ‡ç¤ºå™¨æ¡†æ¶ï¼šhttps://github.com/ongakuer/CircleIndicator
    implementation 'me.relex:circleindicator:2.1.6'

    // è…¾è®¯ MMKVï¼šhttps://github.com/Tencent/MMKV
    implementation 'com.tencent:mmkv-static:1.2.10'

    // å†…å­˜æ³„æ¼ç›‘æµ‹æ¡†æ¶ï¼šhttps://github.com/square/leakcanary
    debugImplementation 'com.squareup.leakcanary:leakcanary-android:2.7'
    previewImplementation 'com.squareup.leakcanary:leakcanary-android:2.7'

    //å¿…é¡»é¡¹ ğŸ‘‡
    implementation 'io.github.FlyJingFish.AndroidAop:android-aop-core:2.2.5'
    //éå¿…é¡»é¡¹ ğŸ‘‡è¿™ä¸ªåŒ…æä¾›äº†ä¸€äº›å¸¸è§çš„æ³¨è§£åˆ‡é¢
    implementation 'io.github.FlyJingFish.AndroidAop:android-aop-extra:2.2.5'

    //éå¿…é¡»é¡¹ ğŸ‘‡ï¼Œå¦‚æœä½ æƒ³è‡ªå®šä¹‰åˆ‡é¢éœ€è¦ç”¨åˆ°ï¼Œâš ï¸æ”¯æŒJavaå’ŒKotlinä»£ç å†™çš„åˆ‡é¢
    ksp 'io.github.FlyJingFish.AndroidAop:android-aop-ksp:2.2.5'

    // å¤šè¯­ç§ï¼šhttps://github.com/getActivity/MultiLanguages
    // æ‚¬æµ®çª—ï¼šhttps://github.com/getActivity/XToast
    // æ—¥å¿—è¾“å‡ºï¼šhttps://github.com/getActivity/Logcat
    // å·¥å…·ç±»ï¼šhttps://github.com/Blankj/AndroidUtilCode
    // è½®æ’­å›¾ï¼šhttps://github.com/bingoogolapple/BGABanner-Android
    // äºŒç»´ç ï¼šhttps://github.com/bingoogolapple/BGAQRCode-Android
    // è·‘é©¬ç¯ï¼šhttps://github.com/sunfusheng/MarqueeView
    // å¯¹è±¡æ³¨è§£ï¼šhttps://www.jianshu.com/p/f1f888e4a35f
    // å¯¹è±¡å­˜å‚¨ï¼šhttps://github.com/leavesC/DoKV
    // å¤šæ¸ é“æ‰“åŒ…ï¼šhttps://github.com/Meituan-Dianping/walle
    // è®¾å¤‡å”¯ä¸€æ ‡è¯†ï¼šhttp://msa-alliance.cn/col.jsp?id=120
    // åµŒå¥—æ»šåŠ¨å®¹å™¨ï¼šhttps://github.com/donkingliang/ConsecutiveScroller
    // éšç§è°ƒç”¨ç›‘æ§ï¼šhttps://github.com/huage2580/PermissionMonitor
}